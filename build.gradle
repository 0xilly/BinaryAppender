plugins {
    id "java"
    id "maven"
    id "signing"
}

group 'io.github.illyohs.binaryappender'
version = project.prog_version
archivesBaseName  = project.prog_name

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    manifest {
        attributes 'Implementation-Title': 'JarAppender',
                'Implementation-Version': version,
                'Main-Class': 'io.github.illyohs.binaryappender.launch.Launch'
    }
}

task fatJar(type: Jar) {
    classifier 'all'
    manifest {
        attributes 'Implementation-Title': 'BinaryAppender',
                'Implementation-Version': version,
                'Main-Class': 'io.github.illyohs.binaryappender.launch.Launch'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.beust:jcommander:1.48"
    compile "commons-io:commons-io:2.5"
    //testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task buildAll(dependsOn:['build', 'javadocJar', 'sourcesJar', 'fatJar'])

artifacts {
    archives javadocJar, sourcesJar, fatJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Example Application'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A small library to append files to the end of binarys'
                url 'https://github.com/Illyohs/BinaryAppender'

                scm {
                    connection 'scm:git:git://github.com:Illyohs/BinaryAppender.git'
                    developerConnection 'git@github.com:Illyohs/BinaryAppender.git'
                    url 'https://github.com/Illyohs/BinaryAppender'
                }

                licenses {
                    license {
                        name 'BSD 2-Clause License'
                        url 'https://github.com/Illyohs/BinaryAppender/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'illyohs'
                        name 'Anthony Anderson'
                        email 'aantony4122@gmail.com'
                    }
                }
            }
        }
    }
}